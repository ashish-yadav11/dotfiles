# Maintainer: Ashish Kumar Yadav <ashishkumar.yadav@students.iiserpune.ac.in>

pkgname=keynav-cust
_pkgname=keynav
pkgver=0.20211018.0
_commit="8ce1f8801a8f179b6eccbedc6089e1b062ef0839"
pkgrel=1
pkgdesc="Ingenious and fast way to move the mouse pointer on the screen with keystrokes"
url="https://github.com/ashish-yadav11/keynav"
license=(BSD)
arch=(x86_64)
depends=(cairo xdotool libxrandr)
provides=(keynav)
conflicts=(keynav)
makedepends=(git perl)
source=("git+https://github.com/ashish-yadav11/keynav#commit=$_commit")
md5sums=(SKIP)

build() {
    cd "$srcdir/$_pkgname"
    make keynav
    make keynav.1
}

package() {
    cd "$srcdir/$_pkgname"
    install -Dm755 keynav    "$pkgdir/usr/bin/keynav"
    install -Dm644 keynavrc  "$pkgdir/etc/keynavrc"
    install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/keynav/COPYRIGHT"
    install -Dm755 keynav.1  "$pkgdir/usr/share/man/man1/keynav.1"
}
